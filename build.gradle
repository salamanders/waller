buildscript {

    ext.kotlin_version = '1.2.21'
    repositories {
        mavenCentral()
        maven {
            url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2'
        }
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-dev"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}

plugins {
    id 'java'
    id 'application'
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
}


sourceCompatibility = 1.8
version = '1.0'
compileJava.options.encoding = 'UTF-8'
mainClassName = 'info.benjaminhill.waller.RobotKt'

jar {
    manifest {
        attributes 'Implementation-Title': 'Waller2',  'Implementation-Version': version, 'Main-Class': mainClassName
    }
    // note this builds one fat jar and it is not recommended for production use - just for illustration purpose
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

configurations {
    sshAntTask
    provided
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'

    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

repositories {
    mavenCentral()
    flatDir {
        dirs '/Users/benjamin/Documents/leJOS_EV3_0.9.1-beta' + '/lib/ev3'
    }
    maven {
        url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2'
    }
    maven {
        url "https://dl.bintray.com/kotlin/kotlin-dev"
    }
}


dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.10.1', 'jsch:jsch:0.1.29'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2.2'
    provided group: 'lejos', name: 'ev3classes', version: '1.0'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: '1.2.21'
}


task deployEV3 {
    doLast {
        ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp', classpath: configurations.sshAntTask.asPath)
        ant.scp(todir: ev3_username + '@' + ev3_server + ':/home/lejos/programs',
                password: ev3_password,
                trust: 'true',
                verbose: 'true') {
            fileset(dir: './build/libs') {
                include(name: '**/*.jar')
            }
        }
    }
}


/*
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
*/